# This module takes the output of input2score-METHOD and generates the
# corresponding BED file by applying some threshold

def get_all_outputs(config):
    out = []
    for f in config['deep_variant']['label']['entry'].keys():
        for m in config['deep_variant']['methods'].keys():
            out.append('output/input2score-deep_variant/{bed}/{model}/{method}/merged.{something}.bed'.format(
                method=m,
                something=f,
                model=config['deep_variant']['model_tag'],
                bed=config['name']))
    for f in config['footprint_annotation'][config['footprint_annotation']['model_tag']].keys():
        for m in config['footprint_annotation']['methods'].keys():
            out.append('output/input2score-footprint_annotation/{bed}/{model}/{method}/merged.{something}.bed'.format(
                method=m,
                something=f,
                model=config['footprint_annotation']['model_tag'],
                bed=config['name']))
    return out

rule all:
    input:
        get_all_outputs(config)

rule score2rawbed:
    input:
        '../{input2score_method}/scores/{bed}/{model_tag}/chunk_{i}.{something}.score.gz'
    output:
        temp('bed/{input2score_method}/{bed}/{model_tag}/{method}/chunk_{i}.{something}.raw.bed.gz')
    params:
        calculator = lambda wildcards: config[wildcards.input2score_method]['methods'][wildcards.method]['calculator'],
        filterr = lambda wildcards: config[wildcards.input2score_method]['methods'][wildcards.method]['filter'],
        param = lambda wildcards: config[wildcards.input2score_method]['methods'][wildcards.method]['param']
    shell:
        'python scripts/score2rawbed.py --input {input[0]} \
        --output {output[0]} \
        --calculator {params.calculator} \
        --filter {params.filterr} \
        --param {params.param}'

rule rawbed2bed:
    input:
        'bed/{input2score_method}/{bed}/{model_tag}/{method}/chunk_{i}.{something}.raw.bed.gz'
    output:
        temp('bed/{input2score_method}/{bed}/{model_tag}/{method}/chunk_{i}.{something}.cleaned.bed.gz')
    shell:
        'bedtools merge -i <(zcat {input[0]}) | gzip > {output[0]}'

rule merge:
    input:
        files = lambda wildcards: [ 'bed/{input2score_method}/{bed}/{model_tag}/{method}/chunk_{i}.{something}.cleaned.bed.gz'.format(i=i, model_tag=wildcards.model_tag, bed=wildcards.bed, something=wildcards.something, input2score_method=wildcards.input2score_method, method=wildcards.method) for i in range(config[wildcards.bed]['number_of_chunk']) ]
    output:
        'output/{input2score_method}/{bed}/{model_tag}/{method}/merged.{something}.bed'
    shell:
        'bedtools sort -i <(zcat {input.files}) | gzip > {output[0]}'








